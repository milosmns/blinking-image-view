apply plugin: 'com.android.library'

/*
 * To upload to Bintray, run:
 * .\gradlew blinkerview:clean blinkerview:install
 * .\gradlew blinkerview:bintrayUpload
 * More information on http://inthecheesefactory.com/blog/how-to-upload-library-to-jcenter-maven-central-as-dependency/en
 * Configuration is in gradle.properties, secures are in local.properties
 */

android {
    compileSdkVersion COMPILE_SDK
    buildToolsVersion BUILD_TOOLS

    defaultConfig {
        minSdkVersion MINIMUM_SDK
        targetSdkVersion TARGET_SDK
        versionCode VERSION_CODE
        versionName VERSION_NAME
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    // this is used in the demo app, don't worry about signing :)
    signingConfigs {
        release {
            keyAlias SIGNING_NAME
            keyPassword SIGNING_NAME
            storeFile file('../signing/keystore.jks')
            storePassword SIGNING_NAME
        }
    }

    // specific options for TravisCI emulators (they take a while to start)
    adbOptions {
        timeOutInMs 20 * 60 * 1000 // 20 minutes
        installOptions "-d", "-t"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation "com.android.support:appcompat-v7:$APPCOMPAT"
}

// Maven push plugin
apply from: '../gradle/gradle-mvn-push.gradle'
